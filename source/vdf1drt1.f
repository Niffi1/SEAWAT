      SUBROUTINE VDF1DRT1FM(NDRTCL,MXDRT,DRTF,HNEW,HCOF,RHS,IBOUND,
     &                      NCOL,NROW,NLAY,NDRTVL,IDRTFL)
C
C-----VERSION 20060606 ERB
C     ******************************************************************
C     ADD DRAIN-RETURN FLOW TO SOURCE TERMS FOR BOTH DRAIN-RETURN CELLS
C     AND RECIPIENT CELLS
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: DENSEREF,PS,ELEV
C
      DOUBLE PRECISION HNEW,EEL
C
      DIMENSION DRTF(NDRTVL,MXDRT),HNEW(NCOL,NROW,NLAY),
     &         RHS(NCOL,NROW,NLAY),IBOUND(NCOL,NROW,NLAY),
     &         HCOF(NCOL,NROW,NLAY)
C--SEAWAT: INCLUDE AUXILIARY VARIABLES
      COMMON /DRTCOM/DRTAUX(5)
      CHARACTER*16 DRTAUX
C     ------------------------------------------------------------------
C
C1------IF NDRTCL<=0 THERE ARE NO DRAINS. RETURN.
      IF (NDRTCL.LE.0) RETURN
C
C--SEAWAT: GET ZDRT IF AUX IS SPECIFIED
		LOCZDRT=0
		DO I=1,5
			IF(DRTAUX(I).EQ.'DRTBELEV') LOCZDRT=I+5
		ENDDO
C
C2------PROCESS EACH CELL IN THE DRAIN-RETURN CELL LIST.
      DO 100 L=1,NDRTCL
C
C3------GET COLUMN, ROW AND LAYER OF CELL CONTAINING DRAIN.
        IL=DRTF(1,L)
        IR=DRTF(2,L)
        IC=DRTF(3,L)
C

C4-------IF THE CELL IS EXTERNAL SKIP IT.
        IF (IBOUND(IC,IR,IL).LE.0) GOTO 100
C
C--SEAWAT: SET ELEVATION THAT DRT HEAD IS REFERENCED FROM
		ZDRT=ELEV(IC,IR,IL)
		IF(LOCZDRT.GT.0) ZDRT=DRTF(LOCZDRT,L)
C
C5-------IF THE CELL IS INTERNAL GET THE DRAIN DATA.
        EL=DRTF(4,L)
C--SEAWAT: SET EEL TO EQUIVALENT FRESHWATER HEAD OF DRT
C        EEL=EL
         EEL=FEHEAD(EL,PS(IC,IR,IL),ZDRT)
C
C6------IF HEAD IS LOWER THAN DRAIN THEN SKIP THIS CELL.
C--SEAWAT: USE SALTHEAD IN CONDITIONAL STATEMENT
C        IF (HNEW(IC,IR,IL).LE.EEL) GOTO 100
        IF (SALTHEAD(HNEW(IC,IR,IL),PS(IC,IR,IL),ELEV(IC,IR,IL))
     +               .LE.EEL) GOTO 100
C
C7------HEAD IS HIGHER THAN DRAIN. ADD TERMS TO RHS AND HCOF.
        C=DRTF(5,L)
C--SEAWAT: USE VARIABLE DENSITY FORM OF EQUATIONS
C        HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-C
C        RHS(IC,IR,IL)=RHS(IC,IR,IL)-C*EL
        HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-C*PS(IC,IR,IL)
	  RHS(IC,IR,IL)=RHS(IC,IR,IL)-C*PS(IC,IR,IL)*
     &                  (EEL-(PS(IC,IR,IL)-DENSEREF)/DENSEREF*
     &                  (ELEV(IC,IR,IL)-ZDRT))
        IF (IDRTFL.GT.0) THEN
          ILR = DRTF(6,L)
          IF (ILR.NE.0) THEN
            IRR = DRTF(7,L)
            ICR = DRTF(8,L)
            IF (IBOUND(ICR,IRR,ILR) .GT. 0) THEN
              RFPROP = DRTF(9,L)
              H = HNEW(IC,IR,IL)
C--SEAWAT: CALCULATE DRAIN FLUX USING VARIABLE DENSITY EQUATION
              QMASS=PS(IC,IR,IL)*C*(EEL-H-
     &         (PS(IC,IR,IL)-DENSEREF)/DENSEREF*(ELEV(IC,IR,IL)-ZDRT))
C            RHS(ICR,IRR,ILR) = RHS(ICR,IRR,ILR)
C     &                         - RFPROP*C*(H-EL)
C--SEAWAT: FLIPPED SIGN BECAUSE QMASS USES (EEL-H)
              RHS(ICR,IRR,ILR) = RHS(ICR,IRR,ILR)
     &                         + RFPROP*QMASS
            ENDIF
          ENDIF
        ENDIF
  100 CONTINUE
C
C8------RETURN.
      RETURN
      END
C=======================================================================
      SUBROUTINE VDF1DRT1BD(NDRTCL,MXDRT,VBNM,VBVL,MSUM,DRTF,DELT,HNEW,
     &                      NCOL,NROW,NLAY,IBOUND,KSTP,KPER,IDRTCB,
     &                      ICBCFL,BUFF,IOUT,PERTIM,TOTIM,NDRTVL,IDRTAL,
     &                      IDRTFL,NRFLOW,IAUXSV)
C-----VERSION 20060606 ERB
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR DRAIN-RETURN CELLS
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: DENSEREF,PS,ELEV
C
      COMMON /DRTCOM/DRTAUX(5)
      CHARACTER*16 DRTAUX
      CHARACTER*16 VBNM(MSUM),TEXT
      DOUBLE PRECISION HNEW,HHNEW,EEL,CC,CEL,RATIN,RATOUT,QQ
C
      DIMENSION VBVL(4,MSUM),DRTF(NDRTVL,MXDRT),HNEW(NCOL,NROW,NLAY),
     &          IBOUND(NCOL,NROW,NLAY),BUFF(NCOL,NROW,NLAY)
C
      DATA TEXT /'    DRAINS (DRT)'/
C     ------------------------------------------------------------------
C
C1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
C1------ACCUMULATORS (RATIN AND RATOUT).
      ZERO=0.
      RATIN=ZERO
      RATOUT=ZERO
      IBD=0
      IF (IDRTCB.LT.0 .AND. ICBCFL.NE.0) IBD=-1
      IF (IDRTCB.GT.0) IBD=ICBCFL
      IBDLBL=0
C
C2------IF CELL-BY-CELL FLOWS WILL BE SAVED AS A LIST, WRITE HEADER.
      IF (IBD.EQ.2) THEN
        NAUX = NDRTVL - 5 - IDRTAL
        IF (IAUXSV.EQ.0) NAUX = 0
        CALL UBDSV4(KSTP,KPER,TEXT,NAUX,DRTAUX,IDRTCB,NCOL,NROW,NLAY,
     &              NDRTCL+NRFLOW,IOUT,DELT,PERTIM,TOTIM,IBOUND)
      ENDIF
C
C3------CLEAR THE BUFFER.
      DO 30 IL=1,NLAY
        DO 20 IR=1,NROW
          DO 10 IC=1,NCOL
            BUFF(IC,IR,IL)=ZERO
   10     CONTINUE
   20   CONTINUE
   30 CONTINUE
C
C4------IF THERE ARE NO DRAIN-RETURN CELLS THEN DO NOT ACCUMULATE FLOW.
      IF (NDRTCL.LE.0) GOTO 200
C
C--SEAWAT: FIND DRTBELEV IF EXISTS AS AUX VARIABLE
      LOCZDRT=0
      DO I=1,5
        IF(DRTAUX(I).EQ.'DRTBELEV') LOCZDRT=I+5
      ENDDO
C
C5------LOOP THROUGH EACH DRAIN-RETURN CELL, CALCULATING FLOW.
      DO 100 L=1,NDRTCL
C
C5A-----GET LAYER, ROW & COLUMN OF CELL CONTAINING DRAIN.
        IL=DRTF(1,L)
        IR=DRTF(2,L)
        IC=DRTF(3,L)
        Q=ZERO
        ILR=0
        IF (IDRTFL.GT.0) THEN
          QIN=ZERO
          ILR = DRTF(6,L)
          IRR = DRTF(7,L)
          ICR = DRTF(8,L)
          IF (IBOUND(ICR,IRR,ILR) .LE. 0) ILR = 0
        END IF
C
C5B-----IF CELL IS NO-FLOW OR CONSTANT-HEAD, IGNORE IT.
        IF (IBOUND(IC,IR,IL).LE.0) GOTO 99
C
C5C-----GET DRAIN PARAMETERS FROM DRAIN-RETURN LIST.
        EL=DRTF(4,L)
C--SEAWAT: SET REFERENCE ELEVATION FOR DRT HEAD
	  ZDRT=ELEV(IC,IR,IL)
	  IF(LOCZDRT.GT.0) ZDRT=DRTF(LOCZDRT,L)
C--SEAWAT: CALCULATE EEL AS EQUIVALENT FRESHWATER VALUE
C        EEL=EL
        EEL=FEHEAD(EL,PS(IC,IR,IL),ZDRT)
        C=DRTF(5,L)
        HHNEW=HNEW(IC,IR,IL)
C
C5D-----IF HEAD HIGHER THAN DRAIN, CALCULATE Q=C*(EL-HHNEW).
C5D-----SUBTRACT Q FROM RATOUT.
C--SEAWAT: USE SALTWATER HEAD AND EL FOR TURNING ON DRT
        IF (SALTHEAD(HHNEW,PS(IC,IR,IL),ELEV(IC,IR,IL)).GT.EL) THEN
          CC=C
C          CEL=C*EL
C          QQ=CEL - CC*HHNEW
C--SEAWAT: VD FORM, CONSERVE MASS
	    QQ=PS(IC,IR,IL)*CC*(EEL-HNEW(IC,IR,IL)-
     &       (PS(IC,IR,IL)-DENSEREF)/DENSEREF*(ELEV(IC,IR,IL)-ZDRT))
          Q=QQ
          RATOUT=RATOUT-QQ
          IF (IDRTFL.GT.0) THEN
            IF (ILR.NE.0) THEN
              RFPROP = DRTF(9,L)
C--SEAWAT: FLIP SIGN AND USE QQ INSTEAD OF (CC*HNEW-CEL)
C              QQIN = RFPROP*(CC*HHNEW-CEL)
              QQIN = -QQ*RFPROP
              QIN = QQIN
              RATIN = RATIN + QQIN
            ENDIF
          ENDIF
        ENDIF
C
C5E-----PRINT THE INDIVIDUAL RATES IF REQUESTED(IDRTCB<0).
        IF (IBD.LT.0) THEN
          IF (IBDLBL.EQ.0) WRITE(IOUT,61) TEXT,KPER,KSTP
   61     FORMAT(1X,/1X,A,'   PERIOD ',I4,'   STEP ',I3)
          WRITE(IOUT,62) L,IL,IR,IC,Q
   62     FORMAT(1X,'DRAIN ',I6,'   LAYER ',I3,'   ROW ',I5,
     &       '   COL ',I5,'   RATE ',1PG15.6)
          IF (ILR.NE.0) THEN
            WRITE(IOUT,550) L,ILR,IRR,ICR,QIN
  550       FORMAT(1X,'DRAIN ',I6,' RETURN:  LAYER ',I3,'   ROW ',I5,
     &       '   COL ',I5,'   RATE ',1PG15.6)
          ENDIF
          IBDLBL=1
        ENDIF
C
C5F-----ADD Q TO BUFFER.
C--SEAWAT: SAVE VOLUMETRIC FLUX INSTEAD OF MASS FLUX
	  Q=Q/PS(IC,IR,IL)
	  QIN=QIN/PS(IC,IR,IL)
        BUFF(IC,IR,IL) = BUFF(IC,IR,IL) + Q
        IF (IDRTFL.GT.0 .AND. ILR.GT.0)
     &      BUFF(ICR,IRR,ILR) = BUFF(ICR,IRR,ILR) + QIN
C
C5G-----IF SAVING CELL-BY-CELL FLOWS IN A LIST, WRITE FLOW.  OR IF
C5G-----RETURNING THE FLOW IN THE DRTF ARRAY, COPY FLOW TO DRTF.
   99   IF (IBD.EQ.2) THEN
          CALL UBDSVB(IDRTCB,NCOL,NROW,IC,IR,IL,Q,DRTF(1,L),NDRTVL,NAUX,
     &                10,IBOUND,NLAY)
          IF (IDRTFL.NE.0 .AND. ILR.GT.0)
     &      CALL UBDSVB(IDRTCB,NCOL,NROW,ICR,IRR,ILR,QIN,DRTF(1,L),
     &                  NDRTVL,NAUX,10,IBOUND,NLAY)
        ENDIF
        IF (IDRTAL.NE.0) DRTF(NDRTVL,L) = Q
        IF (IDRTAL.GT.1) DRTF(NDRTVL-1,L) = QIN
  100 CONTINUE
C
C6------IF CELL-BY-CELL FLOW WILL BE SAVED AS A 3-D ARRAY,
C6------CALL UBUDSV TO SAVE THEM.
      IF (IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IDRTCB,BUFF,NCOL,NROW,
     &                          NLAY,IOUT)
C
C7------MOVE RATES,VOLUMES & LABELS INTO ARRAYS FOR PRINTING.
  200 CONTINUE
      RIN = RATIN
      ROUT=RATOUT
      VBVL(3,MSUM)=RIN
      VBVL(1,MSUM)=VBVL(1,MSUM)+RIN*DELT
      VBVL(4,MSUM)=ROUT
      VBVL(2,MSUM)=VBVL(2,MSUM)+ROUT*DELT
      VBNM(MSUM)=TEXT
C
C8------INCREMENT BUDGET TERM COUNTER.
      MSUM=MSUM+1
C
C9------RETURN.
      RETURN
      END