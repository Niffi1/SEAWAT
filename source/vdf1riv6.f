      SUBROUTINE VDF1RIV6FM(NRIVER,MXRIVR,RIVR,HNEW,HCOF,RHS,IBOUND,
     &                      NCOL,NROW,NLAY,NRIVVL,
     &                      MXSS,NSS,SS,NCOMP,SSMC,NSSVL)
C
C-----VERSION 11JAN2000 GWF1RIV6FM
C     ******************************************************************
C     ADD RIVER TERMS TO RHS AND HCOF
C--SEAWAT: MODIFIED TO USE A VARIABLE DENSITY FORM OF DARCY'S LAW
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: MT3DRHOFLG,DENSEREF,PS,ELEV
C
C--SEAWAT: ADD HRIV
      DOUBLE PRECISION HNEW,RRBOT,HRIV
      DIMENSION RIVR(NRIVVL,MXRIVR),HNEW(NCOL,NROW,NLAY),
     1          HCOF(NCOL,NROW,NLAY),RHS(NCOL,NROW,NLAY),
     2          IBOUND(NCOL,NROW,NLAY)
C--SEAWAT: DIMENSION ADDITIONAL ARRAYS
      DIMENSION SS(NSSVL,MXSS),SSMC(NCOMP,MXSS)
C--SEAWAT: NEED AUXILIARY VARIABLES
      COMMON /RIVCOM/RIVAUX(5)
      CHARACTER*16 RIVAUX
C     ------------------------------------------------------------------
C
C1------IF NRIVER<=0 THERE ARE NO RIVERS. RETURN.
      IF(NRIVER.LE.0)RETURN
C
C--SEAWAT: FIND RBDTHK AND RIVDEN IF EXIST AS AUX VARIABLES
      LOCRBDTHK=0
      DO I=1,5
		IF(RIVAUX(I).EQ.'RBDTHK') LOCRBDTHK=I+6
      ENDDO   
      LOCRIVDEN=0
      DO I=1,5
		IF(RIVAUX(I).EQ.'RIVDEN') LOCRIVDEN=I+6
      ENDDO   

C2------PROCESS EACH CELL IN THE RIVER LIST.
      DO 100 L=1,NRIVER
C
C3------GET COLUMN, ROW, AND LAYER OF CELL CONTAINING REACH.
		IL=RIVR(1,L)
		IR=RIVR(2,L)
		IC=RIVR(3,L)
C
C4------IF THE CELL IS EXTERNAL SKIP IT.
		IF(IBOUND(IC,IR,IL).LE.0)GO TO 100
C
C5------SINCE THE CELL IS INTERNAL GET THE RIVER DATA.
		HRIV=RIVR(4,L)
		CRIV=RIVR(5,L)
		RBOT=RIVR(6,L)
		RRBOT=RBOT
		RBDTHK=ABS(RBOT-ELEV(IC,IR,IL))
		IF(LOCRBDTHK.GT.0) RBDTHK=RIVR(LOCRBDTHK,L)
		RIVDENS=PS(IC,IR,IL)
		IF(LOCRIVDEN.GT.0) RIVDENS=RIVR(LOCRIVDEN,L)
		IF(MT3DRHOFLG.NE.0) RIVDENS=SSMDENSE(IC,IR,IL,4,MXSS,NSS,
     &                           SS,NCOMP,SSMC)
		HRIV=FEHEAD(HRIV,RIVDENS,RBOT+RBDTHK)
		HHNEW=HNEW(IC,IR,IL)
C--SEAWAT: CALCULATE FRESHWATER HEAD AT RBOT USING HEAD IN MODEL CELL
		HFRBOT=HHNEW+(PS(IC,IR,IL)-DENSEREF)/DENSEREF*
     &			(ELEV(IC,IR,IL)-RBOT)
C
C6------COMPARE AQUIFER HEAD TO BOTTOM OF STREAM BED.
C--SEAWAT: USE SALTHEAD FOR THIS COMPARISON
		IF(SALTHEAD(HNEW(IC,IR,IL),PS(IC,IR,IL),ELEV(IC,IR,IL))
     +           .LE.RRBOT) GO TO 96
		RHOAVG=(RIVDENS+PS(IC,IR,IL))/2
		DIRECT=-((HRIV-HFRBOT)+(PS(IC,IR,IL)-DENSEREF)/
     +            DENSEREF*RBDTHK)
C--SEAWAT: DIRECT IS POSITIVE, FLOW IS UP INTO RIVER
		IF (DIRECT.GT.0.) RIVDENS=PS(IC,IR,IL)
C7------SINCE HEAD>BOTTOM ADD TERMS TO RHS AND HCOF.
C--SEAWAT: USE VARIABLE DENSITY FORM OF DARCY'S LAW, CONSERVE MASS
		RHS(IC,IR,IL)=RHS(IC,IR,IL)-CRIV*RIVDENS*
     &				  (HRIV-(PS(IC,IR,IL)-DENSEREF)/DENSEREF*
     &                  (ELEV(IC,IR,IL)-RBOT)+(RHOAVG-DENSEREF)/
     +                  DENSEREF*RBDTHK)
		HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-CRIV*RIVDENS
        GO TO 100
C8------SINCE HEAD<BOTTOM ADD TERM ONLY TO RHS.
C--SEAWAT: USE VARIABLE DENSITY FORM OF DARCY'S LAW, CONSERVE MASS
   96		RHS(IC,IR,IL)=RHS(IC,IR,IL)-CRIV*RIVDENS*
     &                  (HRIV-RBOT+(RIVDENS-DENSEREF)/DENSEREF*RBDTHK)
  100 CONTINUE
C
C9------RETURN
      RETURN
      END

      SUBROUTINE VDF1RIV6BD(NRIVER,MXRIVR,RIVR,IBOUND,HNEW,
     1      NCOL,NROW,NLAY,DELT,VBVL,VBNM,MSUM,KSTP,KPER,IRIVCB,
     2      ICBCFL,BUFF,IOUT,PERTIM,TOTIM,NRIVVL,IRIVAL,IAUXSV,
     3      MXSS,NSS,SS,NCOMP,SSMC,NSSVL)
C-----VERSION 05JUNE2000 GWF1RIV6BD
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR RIVERS
C--SEAWAT: USE VARIABLE-DENSITY EQUATIONS
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: MT3DRHOFLG,DENSEREF,PS,ELEV
C
      COMMON /RIVCOM/RIVAUX(5)
      CHARACTER*16 RIVAUX
      CHARACTER*16 VBNM(MSUM),TEXT
C--SEAWAT: ADD HRIV
      DOUBLE PRECISION HNEW,HHNEW,CHRIV,RRBOT,CCRIV,RATIN,RATOUT,RRATE,
     &                 HRIV
      DIMENSION RIVR(NRIVVL,MXRIVR),IBOUND(NCOL,NROW,NLAY),
     1          HNEW(NCOL,NROW,NLAY),VBVL(4,MSUM),BUFF(NCOL,NROW,NLAY)
      DATA TEXT /'   RIVER LEAKAGE'/
C--SEAWAT: DIMENSION ADDITIONAL ARRAYS
      DIMENSION SS(NSSVL,MXSS),SSMC(NCOMP,MXSS)
C     ------------------------------------------------------------------
C
C1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
C1------ACCUMULATORS (RATIN AND RATOUT).
      ZERO=0.
      RATIN=ZERO
      RATOUT=ZERO
      IBD=0
      IF(IRIVCB.LT.0 .AND. ICBCFL.NE.0) IBD=-1
      IF(IRIVCB.GT.0) IBD=ICBCFL
      IBDLBL=0
C
C2------IF CELL-BY-CELL FLOWS WILL BE SAVED AS A LIST, WRITE HEADER.
      IF(IBD.EQ.2) THEN
         NAUX=NRIVVL-6-IRIVAL
         IF(IAUXSV.EQ.0) NAUX=0
         CALL UBDSV4(KSTP,KPER,TEXT,NAUX,RIVAUX,IRIVCB,NCOL,NROW,NLAY,
     1          NRIVER,IOUT,DELT,PERTIM,TOTIM,IBOUND)
      END IF
C
C3------CLEAR THE BUFFER.
      DO 50 IL=1,NLAY
      DO 50 IR=1,NROW
      DO 50 IC=1,NCOL
		BUFF(IC,IR,IL)=ZERO
50    CONTINUE
C
C4------IF NO REACHES, SKIP FLOW CALCULATIONS.
      IF(NRIVER.EQ.0)GO TO 200
C
C--SEAWAT: FIND AUX VARIABLES FOR RBDTHK AND RIVDEN IF EXISTS
      LOCRBDTHK=0
      DO I=1,5
        IF(RIVAUX(I).EQ.'RBDTHK') LOCRBDTHK=I+6
      ENDDO
	LOCRIVDEN=0
      DO I=1,5
        IF(RIVAUX(I).EQ.'RIVDEN') LOCRIVDEN=I+6
      ENDDO
C
C5------LOOP THROUGH EACH RIVER REACH CALCULATING FLOW.
      DO 100 L=1,NRIVER
C
C5A-----GET LAYER, ROW & COLUMN OF CELL CONTAINING REACH.
       IL=RIVR(1,L)
       IR=RIVR(2,L)
       IC=RIVR(3,L)
       RATE=ZERO
C
C5B-----IF CELL IS NO-FLOW OR CONSTANT-HEAD MOVE ON TO NEXT REACH.
       IF(IBOUND(IC,IR,IL).LE.0)GO TO 99
C
C5C-----GET RIVER PARAMETERS FROM RIVER LIST.
		HRIV=RIVR(4,L)
		CRIV=RIVR(5,L)
		RBOT=RIVR(6,L)
		RRBOT=RBOT
		RBDTHK=ABS(RBOT-ELEV(IC,IR,IL))
		IF(LOCRBDTHK.GT.0) RBDTHK=RIVR(LOCRBDTHK,L)
		RIVDENS=PS(IC,IR,IL)
		IF(LOCRIVDEN.GT.0) RIVDENS=RIVR(LOCRIVDEN,L)
		IF(MT3DRHOFLG.NE.0) RIVDENS=SSMDENSE(IC,IR,IL,4,MXSS,NSS,SS,
     &                           NCOMP,SSMC)   
		HRIV=FEHEAD(HRIV,RIVDENS,RBOT+RBDTHK)
		HHNEW=HNEW(IC,IR,IL)
		HTEMP=SALTHEAD(HHNEW,PS(IC,IR,IL),ELEV(IC,IR,IL))
		HFRBOT=HHNEW+(PS(IC,IR,IL)-DENSEREF)/DENSEREF*
     &		   (ELEV(IC,IR,IL)-RBOT)
C--SEAWAT: COMPARISON DONE WITH SALTHEAD
C5D-----COMPARE HEAD IN AQUIFER TO BOTTOM OF RIVERBED.
		IF(HTEMP.GT.RRBOT) THEN
			RHOAVG=(RIVDENS+PS(IC,IR,IL))/2
			DIRECT=-(HRIV-HFRBOT+(RHOAVG-DENSEREF)/DENSEREF*RBDTHK)
C--SEAWAT:  DIRECT IS POSITIVE, FLOW IS UP INTO RIVER
			IF (DIRECT.GT.0.) RIVDENS=PS(IC,IR,IL)
			RATE=CRIV*RIVDENS*(HRIV-HFRBOT+(RHOAVG-DENSEREF)/
     &			 DENSEREF*RBDTHK)
			RRATE=RATE
		ENDIF
C5F-----AQUIFER HEAD < BOTTOM THEN RATE=CRIV*(HRIV-RBOT).
		IF(HTEMP.LE.RRBOT) THEN
			RATE=CRIV*RIVDENS*(HRIV-RBOT+(RIVDENS-DENSEREF)/
     +			 DENSEREF*RBDTHK)
			RRATE=RATE
		ENDIF
C
C5G-----PRINT THE INDIVIDUAL RATES IF REQUESTED(IRIVCB<0).
		IF(IBD.LT.0) THEN
			IF(IBDLBL.EQ.0) WRITE(IOUT,61) TEXT,KPER,KSTP
   61			FORMAT(1X,/1X,A,'   PERIOD',I4,'   STEP',I3)
C--SEAWAT: WRITE VOLUMETRIC FLUX
			WRITE(IOUT,62) L,IL,IR,IC,RATE/RIVDENS
   62			FORMAT(1X,'REACH',I4,'   LAYER',I3,'   ROW',I5,'   COL',
     +		       I5,'   RATE',1PG15.6)
			IBDLBL=1
		END IF
C
C5H------ADD RATE TO BUFFER.
C--SEAWAT: CONVERT RATE TO VOLUMETRIC FLUX FOR CBB OUTPUT 
C--SEAWAT: RRATE REMAINS MASS FLUX
		RATE=RATE/RIVDENS
		BUFF(IC,IR,IL)=BUFF(IC,IR,IL)+RATE
C
C5I-----SEE IF FLOW IS INTO AQUIFER OR INTO RIVER.
		IF(RATE)94,99,96
C
C5J-----AQUIFER IS DISCHARGING TO RIVER SUBTRACT RATE FROM RATOUT.
   94		RATOUT=RATOUT-RRATE
		GO TO 99
C
C5K-----AQUIFER IS RECHARGED FROM RIVER; ADD RATE TO RATIN.
   96		RATIN=RATIN+RRATE
C
C5L-----IF SAVING CELL-BY-CELL FLOWS IN LIST, WRITE FLOW.  OR IF
C5L-----RETURNING THE FLOW IN THE RIVR ARRAY, COPY FLOW TO RIVR.
   99		IF(IBD.EQ.2) CALL UBDSVB(IRIVCB,NCOL,NROW,IC,IR,IL,RATE,
     1                  RIVR(1,L),NRIVVL,NAUX,7,IBOUND,NLAY)
		IF(IRIVAL.NE.0) RIVR(NRIVVL,L)=RATE
  100 CONTINUE
C
C6------IF CELL-BY-CELL FLOW WILL BE SAVED AS A 3-D ARRAY,
C6------CALL UBUDSV TO SAVE THEM.
      IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IRIVCB,BUFF,NCOL,NROW,
     1                          NLAY,IOUT)
C
C7------MOVE RATES,VOLUMES & LABELS INTO ARRAYS FOR PRINTING.
  200 RIN=RATIN
      ROUT=RATOUT
      VBVL(3,MSUM)=RIN
      VBVL(4,MSUM)=ROUT
      VBVL(1,MSUM)=VBVL(1,MSUM)+RIN*DELT
      VBVL(2,MSUM)=VBVL(2,MSUM)+ROUT*DELT
      VBNM(MSUM)=TEXT
C
C8------INCREMENT BUDGET TERM COUNTER.
      MSUM=MSUM+1
C
C9------RETURN.
      RETURN
      END

