      SUBROUTINE VDF1DRN6FM(NDRAIN,MXDRN,DRAI,HNEW,HCOF,RHS,IBOUND,
     1              NCOL,NROW,NLAY,NDRNVL)
C
C-----VERSION 11JAN2000 DRN5FM
C     ******************************************************************
C     ADD DRAIN FLOW TO SOURCE TERM
C--SEAWAT: MODIFIED FOR VD FLOW
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: DENSEREF,PS,ELEV
C--SEAWAT: ADD EL
      DOUBLE PRECISION HNEW,EEL,EL,HHNEW
C
      DIMENSION DRAI(NDRNVL,MXDRN),HNEW(NCOL,NROW,NLAY),
     1         RHS(NCOL,NROW,NLAY),IBOUND(NCOL,NROW,NLAY),
     1         HCOF(NCOL,NROW,NLAY)
C--SEAWAT: INCLUDE AUXILIARY VARIABLES
      COMMON /DRNCOM/DRNAUX(5)
      CHARACTER*16 DRNAUX
C     ------------------------------------------------------------------
C
C1------IF NDRAIN<=0 THERE ARE NO DRAINS. RETURN.
      IF(NDRAIN.LE.0) RETURN
C
C--SEAWAT: GET ZDRN IF AUX IS SPECIFIED
        LOCZDRN=0
        DO I=1,5
            IF(DRNAUX(I).EQ.'DRNBELEV') LOCZDRN=I+5
        ENDDO
C2------PROCESS EACH CELL IN THE DRAIN LIST.
      DO 100 L=1,NDRAIN
C
C3------GET COLUMN, ROW AND LAYER OF CELL CONTAINING DRAIN.
        IL=DRAI(1,L)
        IR=DRAI(2,L)
        IC=DRAI(3,L)
C
C4-------IF THE CELL IS EXTERNAL SKIP IT.
        IF(IBOUND(IC,IR,IL).LE.0) GO TO 100
C
C5-------IF THE CELL IS INTERNAL GET THE DRAIN DATA.
        EL=DRAI(4,L)
        ZDRN=ELEV(IC,IR,IL)
        IF(LOCZDRN.GT.0) ZDRN=DRAI(LOCZDRN,L)
C--SEAWAT: MAKE EEL EQUIVALENT FRESHWATER
        EEL=FEHEAD(EL,PS(IC,IR,IL),ZDRN)
C
C6------IF HEAD IS LOWER THAN DRAIN THEN SKIP THIS CELL.
C--SEAWAT:USE SALTHEAD COMPARISONS
        IF(SALTHEAD(HNEW(IC,IR,IL),PS(IC,IR,IL),ELEV(IC,IR,IL))
     +               .LE.EL) GO TO 100
C
C7------HEAD IS HIGHER THAN DRAIN. ADD TERMS TO RHS AND HCOF.
C--SEAWAT: USE VD FORM,CONSERVE MASS
        C=DRAI(5,L)
        HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-C*PS(IC,IR,IL)
        RHS(IC,IR,IL)=RHS(IC,IR,IL)-C*PS(IC,IR,IL)*
     &                  (EEL-(PS(IC,IR,IL)-DENSEREF)/DENSEREF*
     &                  (ELEV(IC,IR,IL)-ZDRN))

  100 CONTINUE
C
C8------RETURN.
      RETURN
      END
      SUBROUTINE VDF1DRN6BD(NDRAIN,MXDRN,VBNM,VBVL,MSUM,DRAI,DELT,HNEW,
     1        NCOL,NROW,NLAY,IBOUND,KSTP,KPER,IDRNCB,ICBCFL,BUFF,IOUT,
     2        PERTIM,TOTIM,NDRNVL,IDRNAL,IAUXSV)
C-----VERSION 05JUNE2000 GWF1DRN6BD
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR DRAINS
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: DENSEREF,PS,ELEV
C
      COMMON /DRNCOM/DRNAUX(5)
      CHARACTER*16 DRNAUX
      CHARACTER*16 VBNM(MSUM),TEXT
C--SEAWAT: ADD EL
      DOUBLE PRECISION HNEW,HHNEW,EEL,CC,CEL,RATOUT,QQ,EL
C
      DIMENSION VBVL(4,MSUM),DRAI(NDRNVL,MXDRN),HNEW(NCOL,NROW,NLAY),
     1          IBOUND(NCOL,NROW,NLAY),BUFF(NCOL,NROW,NLAY)
C
      DATA TEXT /'          DRAINS'/
C     ------------------------------------------------------------------
C
C1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
C1------ACCUMULATOR (RATOUT).
      ZERO=0.
      RATOUT=ZERO
      IBD=0
      IF(IDRNCB.LT.0 .AND. ICBCFL.NE.0) IBD=-1
      IF(IDRNCB.GT.0) IBD=ICBCFL
      IBDLBL=0
C
C2------IF CELL-BY-CELL FLOWS WILL BE SAVED AS A LIST, WRITE HEADER.
      IF(IBD.EQ.2) THEN
         NAUX=NDRNVL-5-IDRNAL
         IF(IAUXSV.EQ.0) NAUX=0
         CALL UBDSV4(KSTP,KPER,TEXT,NAUX,DRNAUX,IDRNCB,NCOL,NROW,NLAY,
     1          NDRAIN,IOUT,DELT,PERTIM,TOTIM,IBOUND)
      END IF
C
C3------CLEAR THE BUFFER.
      DO 50 IL=1,NLAY
      DO 50 IR=1,NROW
      DO 50 IC=1,NCOL
      BUFF(IC,IR,IL)=ZERO
50    CONTINUE
C
C4------IF THERE ARE NO DRAINS THEN DO NOT ACCUMULATE DRAIN FLOW.
      IF(NDRAIN.LE.0) GO TO 200
C
C--SEAWAT: FIND DRNBELEV IF EXISTS AS AUX VARIABLE
      LOCZDRN=0
      DO I=1,5
        IF(DRNAUX(I).EQ.'DRNBELEV') LOCZDRN=I+5
      ENDDO
C
C5------LOOP THROUGH EACH DRAIN CALCULATING FLOW.
      DO 100 L=1,NDRAIN
C
C5A-----GET LAYER, ROW & COLUMN OF CELL CONTAINING REACH.
        IL=DRAI(1,L)
        IR=DRAI(2,L)
        IC=DRAI(3,L)
        Q=ZERO
C
C5B-----IF CELL IS NO-FLOW OR CONSTANT-HEAD, IGNORE IT.
        IF(IBOUND(IC,IR,IL).LE.0) GO TO 99
C
C5C-----GET DRAIN PARAMETERS FROM DRAIN LIST.
        EL=DRAI(4,L)
        ZDRN=ELEV(IC,IR,IL)
        IF(LOCZDRN.GT.0) ZDRN=DRAI(LOCZDRN,L)
        EEL=FEHEAD(EL,PS(IC,IR,IL),ZDRN)
        C=DRAI(5,L)
C--SEAWAT: USE SALTWATER HEAD FOR TURNING DRAIN ON
C5D-----IF HEAD HIGHER THAN DRAIN, CALCULATE Q=C*(EL-HHNEW).
C5D-----SUBTRACT Q FROM RATOUT.
        IF(SALTHEAD(HNEW(IC,IR,IL),PS(IC,IR,IL),ELEV(IC,IR,IL))
     +                .GT.EL) THEN
            CC=C
C--SEAWAT: VD FORM, CONSERVE MASS
            QQ=PS(IC,IR,IL)*CC*(EEL-HNEW(IC,IR,IL)-
     &           (PS(IC,IR,IL)-DENSEREF)/DENSEREF*(ELEV(IC,IR,IL)-ZDRN))
            Q=QQ
            RATOUT=RATOUT-QQ
        END IF
C
C5E-----PRINT THE INDIVIDUAL RATES IF REQUESTED(IDRNCB<0).
        IF(IBD.LT.0) THEN
            IF(IBDLBL.EQ.0) WRITE(IOUT,61) TEXT,KPER,KSTP
   61       FORMAT(1X,/1X,A,'   PERIOD',I4,'   STEP',I3)
C--SEAWAT: WRITE VOLUMETRIC FLUX
            WRITE(IOUT,62) L,IL,IR,IC,Q/PS(IC,IR,IL)
   62       FORMAT(1X,'DRAIN',I4,'   LAYER',I3,'   ROW',I5,'   COL',
     1             I5,'   RATE',1PG15.6)
            IBDLBL=1
        END IF
C
C5F-----ADD Q TO BUFFER.
C--SEAWAT: SAVE AS VOLUMETRIC FLUX
        Q=Q/PS(IC,IR,IL)
        BUFF(IC,IR,IL)=BUFF(IC,IR,IL)+Q
C
C5G-----IF SAVING CELL-BY-CELL FLOWS IN A LIST, WRITE FLOW.  OR IF
C5G-----RETURNING THE FLOW IN THE DRAI ARRAY, COPY FLOW TO DRAI.
   99   IF(IBD.EQ.2) CALL UBDSVB(IDRNCB,NCOL,NROW,IC,IR,IL,Q,
     1                  DRAI(1,L),NDRNVL,NAUX,6,IBOUND,NLAY)
        IF(IDRNAL.NE.0) DRAI(NDRNVL,L)=Q
  100 CONTINUE
C
C6------IF CELL-BY-CELL FLOW WILL BE SAVED AS A 3-D ARRAY,
C6------CALL UBUDSV TO SAVE THEM.
      IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IDRNCB,BUFF,NCOL,NROW,
     1                          NLAY,IOUT)
C
C7------MOVE RATES,VOLUMES & LABELS INTO ARRAYS FOR PRINTING.
  200 ROUT=RATOUT
      VBVL(3,MSUM)=ZERO
      VBVL(4,MSUM)=ROUT
      VBVL(2,MSUM)=VBVL(2,MSUM)+ROUT*DELT
      VBNM(MSUM)=TEXT
C
C8------INCREMENT BUDGET TERM COUNTER.
      MSUM=MSUM+1
C
C9------RETURN.
      RETURN
      END
