      SUBROUTINE RIV1AL(ISUM,LENX,LCRIVR,MXRIVR,NRIVER,IN,IOUT,
     1                      IRIVCB,IRBDTHK)
C
C-----VERSION 1554 12MAY1987 RIV1AL
C     ******************************************************************
C     ALLOCATE ARRAY STORAGE FOR RIVERS
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     ------------------------------------------------------------------
C
C1------IDENTIFY PACKAGE AND INITIALIZE NRIVER.
      WRITE(IOUT,1)IN
    1 FORMAT(1H0,'RIV1 -- RIVER PACKAGE, VERSION 1, 9/1/87',
     1' INPUT READ FROM UNIT',I3)
      NRIVER=0
C
C2------READ & PRINT MXRIVR & IRIVCB(UNIT OR FLAG FOR C-B-C FLOWS)
C--SEAWAT: ADDING IRBDTHK
      READ(IN,2)MXRIVR,IRIVCB,IRBDTHK
    2 FORMAT(3I10)
      WRITE(IOUT,3)MXRIVR
    3 FORMAT(1H ,'MAXIMUM OF',I5,' RIVER NODES')
      IF(IRIVCB.GT.0) WRITE(IOUT,9) IRIVCB
    9 FORMAT(1X,'CELL-BY-CELL FLOWS WILL BE RECORDED ON UNIT',I3)
      IF(IRIVCB.LT.0) WRITE(IOUT,8)
    8 FORMAT(1X,'CELL-BY-CELL FLOWS WILL BE PRINTED')
C
C3------SET LCRIVR EQUAL TO ADDRESS OF FIRST UNUSED SPACE IN X.
      LCRIVR=ISUM
C
C4------CALCULATE AMOUNT OF SPACE USED BY RIVER LIST.
C4S1--SEAWAT: INCREASED ISP*6 TO ISP*7 TO STORE DENSITY
      ISP=8*MXRIVR
      ISUM=ISUM+ISP
C
C5------PRINT AMOUNT OF SPACE USED BY RIVER PACKAGE.
      WRITE (IOUT,4)ISP
    4 FORMAT(1X,I8,' ELEMENTS IN X ARRAY ARE USED FOR RIVERS')
      ISUM1=ISUM-1
      WRITE(IOUT,5)ISUM1,LENX
    5 FORMAT(1X,I8,' ELEMENTS OF X ARRAY USED OUT OF',I8)
      IF(ISUM1.GT.LENX) WRITE(IOUT,6)
    6 FORMAT(1X,'   ***X ARRAY MUST BE DIMENSIONED LARGER***')
C
C7------RETURN
      RETURN
      END
      SUBROUTINE RIV1RP(RIVR,NRIVER,MXRIVR,IN,IOUT,MXSS,NSS,SS,IRBDTHK,
     &                  ELEV,NCOL,NROW,NLAY)
C
C
C-----VERSION 1319 25AUG1982 RIV1RP
C     ******************************************************************
C     READ RIVER HEAD, CONDUCTANCE AND BOTTOM ELEVATION
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)

C0S1--SEAWAT:  INCREASED RIVR(6,MXRIVR) TO RIVR(7,MXRIVR)
      DIMENSION RIVR(8,MXRIVR)

C0S2--SEAWAT: DIMENSION
	DIMENSION SS(6,MXSS),ELEV(NCOL,NROW,NLAY)

C     ------------------------------------------------------------------
C
C
C1------READ ITMP(NUMBER OF RIVER REACHES OR FLAG TO REUSE DATA)
      READ(IN,8)ITMP
    8 FORMAT(I10)
C
C2------TEST ITMP.
      IF(ITMP.GE.0)GO TO 50
C
C2A-----IF ITMP <0 THEN REUSE DATA FROM LAST STRESS PERIOD.
      WRITE(IOUT,7)
    7 FORMAT(1H0,'REUSING RIVER REACHES FROM LAST STRESS PERIOD')
      GO TO 260
C
C3------IF ITMP=> ZERO THEN IT IS THE NUMBER OF RIVER REACHES
   50 NRIVER=ITMP
C
C4------IF NRIVER>MXRIVR THEN STOP.
      IF(NRIVER.LE.MXRIVR)GO TO 100
      WRITE(IOUT,99)NRIVER,MXRIVR
   99 FORMAT(1H0,'NRIVER(',I4,') IS GREATER THAN MXRIVR(',I4,')')
C
C4A-----ABNORMAL STOP.
      STOP
C
C5------PRINT NUMBER OF RIVER REACHES IN THIS STRESS PERIOD.
  100 WRITE(IOUT,1)NRIVER
    1 FORMAT(1H0,//1X,I5,' RIVER REACHES')
C
C6------IF THERE ARE NO RIVER REACHES THEN RETURN.
      IF(NRIVER.EQ.0) GO TO 260
C
C7------READ AND PRINT DATA FOR EACH RIVER REACH.
      WRITE(IOUT,3)
C7S1--SEAWAT: ADDING DENSITY AND RBDTHK TO RIVER OUTPUT LIST
    3 FORMAT(1H0,15X,'LAYER',5X,'ROW',5X,'COL   '
     1,'  STAGE     CONDUCTANCE   BOTTOM ELEVATION      DENSITY   ',
     2'RBDTHK    RIVER REACH'/1X,15X,103('-'))
      DO 250 II=1,NRIVER
      IF (IRBDTHK.EQ.0) THEN
		READ(IN,4)K,I,J,RIVR(4,II),RIVR(5,II),RIVR(6,II)
		RIVR(8,II)=ABS(RIVR(5,II)-ELEV(J,I,K))
	ELSE
		READ(IN,4)K,I,J,RIVR(4,II),RIVR(5,II),RIVR(6,II),RIVR(8,II)
	ENDIF
    4 FORMAT(3I10,4F10.0)
      RIVR(1,II)=K
      RIVR(2,II)=I
      RIVR(3,II)=J

C7S2--SEAWAT: PUT THE DENSITY OF THE RIVER IN RIVR(7,II)
	RIVR(7,II)=SSMDENSE(I,J,K,4,MXSS,NSS,SS)

C7S3--SEAWAT: INCLUDING RIVER DENSITY IN OUTPUT LIST
      WRITE(IOUT,5)K,I,J,RIVR(4,II),RIVR(5,II),RIVR(6,II),RIVR(7,II),
     +             RIVR(8,II),II
    5 FORMAT(1X,15X,I4,I9,I8,G13.4,G14.4,G19.4,G16.4,G10.3,I10)


  250 CONTINUE
C
C8------RETURN
  260 RETURN
      END
      SUBROUTINE RIV1FM(NRIVER,MXRIVR,RIVR,HNEW,HCOF,RHS,IBOUND,
     1                  NCOL,NROW,NLAY,PS,ELEV)
C
C-----VERSION 0915 27AUG1982 RIV1FM
C     ******************************************************************
C     ADD RIVER TERMS TO RHS AND HCOF
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
C
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION RIVR(8,MXRIVR),HNEW(NCOL,NROW,NLAY),
     1          HCOF(NCOL,NROW,NLAY),RHS(NCOL,NROW,NLAY),
     2          IBOUND(NCOL,NROW,NLAY)

C0S1--SEAWAT: DIMENSION
	DIMENSION PS(NCOL,NROW,NLAY),ELEV(NCOL,NROW,NLAY)

C0S2--SEAWAT COMMON
      COMMON   /DENSITY/ PF,CONVERSION
C     ------------------------------------------------------------------
C
C
C1------IF NRIVER<=0 THERE ARE NO RIVERS. RETURN.
      IF(NRIVER.LE.0)RETURN
C
C2------PROCESS EACH CELL IN THE RIVER LIST.
      DO 100 L=1,NRIVER
C
C3------GET COLUMN, ROW, AND LAYER OF CELL CONTAINING REACH
      IL=RIVR(1,L)
      IR=RIVR(2,L)
      IC=RIVR(3,L)
	ZIJK=ELEV(IC,IR,IL)
	RHOIJK=PS(IC,IR,IL)
C
C4------IF THE CELL IS EXTERNAL SKIP IT.
      IF(IBOUND(IC,IR,IL).LE.0)GO TO 100
C
C5------SINCE THE CELL IS INTERNAL GET THE RIVER DATA.
      HRIV=RIVR(4,L)
      CRIV=RIVR(5,L)
      RBOT=RIVR(6,L)
	RBDTHK=RIVR(8,L)
      HHNEW=HNEW(IC,IR,IL)

C5S1--SEAWAT: CALCULATE SALTWATER HEAD AND RIVER DENSITY
	HTEMP=SALTHEAD(HHNEW,RHOIJK,ZIJK)
        DENSE=RIVR(7,L)

C--SEAWAT: CALCULATE FRESHWATER HEAD AT RBOT USING HEAD IN MODEL CELL
	HFRBOT=HHNEW+(RHOIJK-PF)/PF*(ZIJK-RBOT)
C
C6------COMPARE AQUIFER HEAD TO BOTTOM OF STREAM BED.
C6S1--SEAWAT: COMPARE SALTHEAD WITH BOTTOM OF STREAM BED
      IF(HTEMP.LE.RBOT)GO TO 96
C
C7------SINCE HEAD>BOTTOM ADD TERMS TO RHS AND HCOF.
C7S1--SEAWAT: CALCULATE DIRECTION OF FLOW
        RHOAVG=(DENSE+RHOIJK)/2
	DIRECT=-(HRIV-HFRBOT+(RHOAVG-PF)/PF*RBDTHK)
C--SEAWAT:  DIRECT IS POSITIVE, FLOW IS UP INTO RIVER
C--SEAWAT:  DIRECT IS NEGATIVE, FLOW IS DOWN INTO GROUNDWATER
        IF (DIRECT.GT.0.) DENSE=RHOIJK

C7S2--SEAWAT: CONVERT FROM VOLUME TO MASS
        RHS(IC,IR,IL)=RHS(IC,IR,IL)-DENSE*CRIV*(HRIV-(RHOIJK-PF)/PF*
     +              (ZIJK-RBOT)+(RHOAVG-PF)/PF*RBDTHK)
      HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-CRIV*DENSE
      GO TO 100
C
C8------SINCE HEAD<BOTTOM ADD TERM ONLY TO RHS.	
   96   RHS(IC,IR,IL)=RHS(IC,IR,IL)-DENSE*CRIV*(HRIV-RBOT+
     +              (DENSE-PF)/PF*RBDTHK)

  100 CONTINUE
C
C9------RETURN
      RETURN
      END
      SUBROUTINE RIV1BD(NRIVER,MXRIVR,RIVR,IBOUND,HNEW,
     1      NCOL,NROW,NLAY,DELT,VBVL,VBNM,MSUM,ntr,KSTP,KPER,IRIVCB,
     2      ICBCFL,BUFF,IOUT,PS,ELEV,PRTOUT,STEPEND)
C-----VERSION 1556 12MAY1987 RIV1BD
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR RIVERS
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
CS00--SEAWAT: DELT=DTRANS, NTR:NUMBER OF TRANSPORT STEPS
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*4 VBNM,TEXT

C0S1--SEAWAT: LOGICAL
        LOGICAL PRTOUT,STEPEND

      DIMENSION RIVR(8,MXRIVR),IBOUND(NCOL,NROW,NLAY),
     1          HNEW(NCOL,NROW,NLAY),VBVL(4,20),VBNM(4,20),
     2          BUFF(NCOL,NROW,NLAY)

C0S2--SEAWAT: DIMENSION
	DIMENSION PS(NCOL,NROW,NLAY),ELEV(NCOL,NROW,NLAY)

      DIMENSION TEXT(4)

C0S3--SEAWAT: COMMON
	COMMON /DENSITY/ PF,CONVERSION

      DATA TEXT(1),TEXT(2),TEXT(3),TEXT(4) /'   R','IVER',' LEA','KAGE'/
C     ------------------------------------------------------------------
C
C1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
C1------ACCUMULATORS (RATIN AND RATOUT).
      IBD=0

C1S1--SEAWAT: SET IBD
      IF (ICBCFL.NE.0.AND.IRIVCB.GT.0.AND.STEPEND) IBD=1

      RATIN=0.
      RATOUT=0.
C
C2------IF NO REACHES KEEP ZEROES IN ACCUMULATORS.
      IF(NRIVER.EQ.0)GO TO 200
C
C3------TEST TO SEE IF CELL-BY-CELL FLOW TERMS ARE NEEDED.
C3A-----CELL-BY-CELL FLOW TERMS ARE NEEDED SET IBD AND CLEAR BUFFER.
      DO 5 IL=1,NLAY
      DO 5 IR=1,NROW
      DO 5 IC=1,NCOL
      BUFF(IC,IR,IL)=0.
    5 CONTINUE
C
C4------FOR EACH RIVER REACH ACCUMULATE RIVER FLOW (STEPS 5-15)
   10 DO 100 L=1,NRIVER
C
C5------GET LAYER, ROW & COLUMN OF CELL CONTAINING REACH.
      IL=RIVR(1,L)
      IR=RIVR(2,L)
      IC=RIVR(3,L)
C
C6------IF CELL IS EXTERNAL MOVE ON TO NEXT REACH.
      IF(IBOUND(IC,IR,IL).LE.0)GO TO 100
C
C7------GET RIVER PARAMETERS FROM RIVER LIST.
      HRIV=RIVR(4,L)
      CRIV=RIVR(5,L)
      RBOT=RIVR(6,L)
      HHNEW=HNEW(IC,IR,IL)
	RBDTHK=RIVR(8,L)
	ZIJK=ELEV(IC,IR,IL)
	RHOIJK=PS(IC,IR,IL)


C7S1--SEAWAT: FIND SALTHEAD AND DENSEITY OF RIVER WATER
	HTEMP=SALTHEAD(HHNEW,PS(IC,IR,IL),ELEV(IC,IR,IL))
        DENSE=RIVR(7,L)

C--SEAWAT: CALCULATE FRESHWATER HEAD AT RBOT USING HEAD IN MODEL CELL
	HFRBOT=HHNEW+(RHOIJK-PF)/PF*(ZIJK-RBOT)
C
C
C8------COMPARE HEAD IN AQUIFER TO BOTTOM OF RIVERBED.
C
C9------AQUIFER HEAD > BOTTOM THEN RATE=CRIV*(HRIV-HNEW).
C9S1--SEAWAT: COMPARE SALTHEAD WITH RBOT
      IF(HTEMP.GT.RBOT) THEN
        RHOAVG=(DENSE+PS(IC,IR,IL))/2
	DIRECT=-(HRIV-HFRBOT+(RHOAVG-PF)/PF*RBDTHK)
C--SEAWAT:  DIRECT IS POSITIVE, FLOW IS UP INTO RIVER
C--SEAWAT:  DIRECT IS NEGATIVE, FLOW IS DOWN INTO GROUNDWATER
        IF (DIRECT.GT.0.) DENSE=PS(IC,IR,IL)
        RATE=DENSE*CRIV*(HRIV-HFRBOT+(RHOAVG-PF)/PF*RBDTHK)
	ENDIF
C
C10-----AQUIFER HEAD < BOTTOM THEN RATE=CRIV*(HRIV-RBOT)
C10S1--SEAWAT: CONVERT FROM VOLUME TO MASS
      IF(HTEMP.LE.RBOT) RATE=DENSE*CRIV*(HRIV-RBOT+(DENSE-PF)
     *                       /PF*RBDTHK)
C
C11-----PRINT THE INDIVIDUAL RATES IF REQUESTED(IRIVCB<0).
      IF(IRIVCB.LT.0.AND.ICBCFL.NE.0) WRITE(IOUT,900) (TEXT(N),N=1,4),
     1    KPER,KSTP,L,IL,IR,IC,RATE
  900 FORMAT(1H0,4A4,'   PERIOD',I3,'   STEP',I3,'   REACH',I4,
     1    '   LAYER',I3,'   ROW',I4,'   COL',I4,'   RATE',G15.7)
C
C12------IF C-B-C FLOW TERMS ARE TO BE SAVED THEN ADD RATE TO BUFFER.
C12S1--SEAWAT: CONVERT BACK TO VOLUME FLUX TO STORE IN BUFF
      IF(IBD.EQ.1) BUFF(IC,IR,IL)=BUFF(IC,IR,IL)+RATE/DENSE
C
C13-----SEE IF FLOW IS INTO AQUIFER OR INTO RIVER.
      IF(RATE)94,100,96
C
C14-----AQUIFER IS DISCHARGING TO RIVER SUBTRACT RATE FROM RATOUT.
   94 RATOUT=RATOUT-RATE
      GO TO 100
C
C15-----AQUIFER IS RECHARGED FROM RIVER ADD RATE TO RATIN.
   96 RATIN=RATIN+RATE
  100 CONTINUE
C
C16-----IF C-B-C FLOW TERMS WILL BE SAVED CALL UBUDSV TO RECORD THEM.
      IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IRIVCB,BUFF,NCOL,NROW,
     1                          NLAY,IOUT)
C
C17-----MOVE RATES,VOLUMES & LABELS INTO ARRAYS FOR PRINTING.
  200 VBVL(3,MSUM)=RATIN
      VBVL(4,MSUM)=RATOUT
      VBVL(1,MSUM)=VBVL(1,MSUM)+RATIN*DELT
      VBVL(2,MSUM)=VBVL(2,MSUM)+RATOUT*DELT
      VBNM(1,MSUM)=TEXT(1)
      VBNM(2,MSUM)=TEXT(2)
      VBNM(3,MSUM)=TEXT(3)
      VBNM(4,MSUM)=TEXT(4)
C
C18-----INCREMENT BUDGET TERM COUNTER
      MSUM=MSUM+1
C
C19-----RETURN
      RETURN
      END
