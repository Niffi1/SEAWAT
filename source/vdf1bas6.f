C--SEAWAT: PRINT MASS BUDGET, PASS DTRANS,N, IN HERE?
      SUBROUTINE VDF1BAS6OT(HNEW,STRT,ISTRT,BUFF,IOFLG,MSUM,IBOUND,VBNM,
     1  VBVL,KSTP,KPER,DELT,PERTIM,TOTIM,ITMUNI,NCOL,NROW,NLAY,ICNVG,
     2  IHDDFL,IBUDFL,IHEDFM,IHEDUN,IDDNFM,IDDNUN,IOUT,CHEDFM,CDDNFM,
     3  IXSEC,LBHDSV,LBDDSV,IBOUUN,LBBOSV,CBOUFM,ISA,RESETDD,
     4  INVDF,INBTN,DTRANS,N,HT1,HT2)
C-----VERSION 11JAN2000 GWF1BAS6OT
C     ******************************************************************
C     OUTPUT TIME, MASS BUDGET, HEAD, AND DRAWDOWN
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      CHARACTER*16 VBNM(MSUM)
      DOUBLE PRECISION HNEW
      DIMENSION HNEW(NCOL,NROW,NLAY),STRT(NCOL,NROW,NLAY),
     1          VBVL(4,MSUM),IOFLG(NLAY,5),IBOUND(NCOL,NROW,NLAY),
     2          BUFF(NCOL,NROW,NLAY)
      CHARACTER*20 CHEDFM,CDDNFM,CBOUFM
      LOGICAL RESETDD
C     ------------------------------------------------------------------
C
C1------CLEAR PRINTOUT FLAG (IPFLG)
      IPFLG=0
C
C
      IF(ISA.EQ.0) THEN
         WRITE(IOUT,9) KSTP,KPER
    9    FORMAT(1X,/11X,'NO FLOW EQUATION TO SOLVE IN TIME STEP',I3,
     1      ' OF STRESS PERIOD',I3,/1X,'ALL HEADS ARE 0.0')
         IPFLG=1
      END IF
C
C2------IF ITERATIVE PROCEDURE FAILED TO CONVERGE PRINT MESSAGE
      IF(ICNVG.EQ.0) THEN
         WRITE(IOUT,17) KSTP,KPER
   17    FORMAT(1X,/11X,'****FAILED TO CONVERGE IN TIME STEP',I3,
     1      ' OF STRESS PERIOD',I4,'****')
         IPFLG=1
      END IF
C
C3------IF HEAD AND DRAWDOWN FLAG (IHDDFL) IS SET WRITE HEAD,
C3------DRAWDOWN, AND IBOUND IN ACCORDANCE WITH FLAGS IN IOFLG.
      IF(IHDDFL.EQ.0) GO TO 100
C
      CALL SGWF1BAS6H(HNEW,BUFF,IOFLG,KSTP,KPER,NCOL,NROW,NLAY,IOUT,
     1    IHEDFM,IHEDUN,IPFLG,PERTIM,TOTIM,CHEDFM,IXSEC,LBHDSV,IBOUND,
     2    ISA)
      CALL SGWF1BAS6D(HNEW,BUFF,IOFLG,KSTP,KPER,NCOL,NROW,NLAY,IOUT,
     1 IDDNFM,IDDNUN,STRT,ISTRT,IBOUND,IPFLG,PERTIM,TOTIM,CDDNFM,IXSEC,
     2 LBDDSV,ISA)
      CALL SGWF1BAS6IB(IOFLG,KSTP,KPER,NCOL,NROW,NLAY,IOUT,
     1   IBOUUN,PERTIM,TOTIM,CBOUFM,IXSEC,LBBOSV,IBOUND)
C
  100 CONTINUE
C
C     IF "DDREFERENCE" OPTION IS SET FOR THIS TIME STEP, COPY HNEW INTO
C     STRT
  103 FORMAT(/,1X,'DDREFERENCE option selected:  Resetting STRT',
     &' array to heads calculated for',/,1X,
     &'Stress Period ',I5,',    Time Step ',I5,'.',
     &'  Subsequent drawdowns will be',/,' calculated',
     &' relative to heads calculated this time step.',/)
      IF (RESETDD) THEN
        WRITE(IOUT,103)KPER,KSTP
        DO 115 K=1,NLAY
          DO 110 I=1,NROW
            DO 105 J=1,NCOL
              STRT(J,I,K)=HNEW(J,I,K)
  105       CONTINUE
  110     CONTINUE
  115   CONTINUE
        RESETDD=.FALSE.
      ENDIF
  
C4------PRINT TOTAL BUDGET IF REQUESTED
C--SEAWAT: CALL MASS BUDGET ROUTINE IF VDF ACTIVE
      IF(IBUDFL.EQ.0) GO TO 120
      IF(INVDF.GT.0) THEN
        CALL SVDF1BAS6V(MSUM,VBNM,VBVL,KSTP,KPER,IOUT)
      ELSE
        CALL SGWF1BAS6V(MSUM,VBNM,VBVL,KSTP,KPER,IOUT)
      ENDIF
      IPFLG=1
C
C5------END PRINTOUT WITH TIME SUMMARY AND FORM FEED IF ANY PRINTOUT
C5------WILL BE PRODUCED.
  120 IF(IPFLG.EQ.0) RETURN

C--SEAWAT: CALL EXTENDED TIME SUMMARY IF MT3D PROCESS IS ACTIVE
      IF (INBTN.EQ.0) THEN
        CALL SGWF1BAS6T(KSTP,KPER,DELT,PERTIM,TOTIM,ITMUNI,IOUT)
      ELSE
        CALL SVDF1BAS6T(KSTP,KPER,DELT,PERTIM,TOTIM,ITMUNI,IOUT,
     &                DTRANS,N,HT1,HT2)
      ENDIF

      WRITE(IOUT,101)
  101 FORMAT('1')
C
C6------RETURN
      RETURN
      END

      SUBROUTINE SVDF1BAS6T(KSTP,KPER,DELT,PERTIM,TOTIM,ITMUNI,IOUT,
     &                      DTRANS,NTR,HT1,HT2)
C
C-----VERSION 11JAN2000 SGWF1BAS6T
C     ******************************************************************
C     PRINT SIMULATION TIME
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
C     ------------------------------------------------------------------
      WRITE(IOUT,199) NTR,KSTP,KPER
C0S1--SEAWAT: CHANGING 'TIME' TO 'TRANSPORT'
  199 FORMAT(1H0,///10X,'TIME SUMMARY AT END OF TRANSPORT STEP',I5,
     1     ' IN TIME STEP',I5,' IN STRESS PERIOD',I4)

C      WRITE(IOUT,199) KSTP,KPER
C  199 FORMAT(1X,///10X,'TIME SUMMARY AT END OF TIME STEP',I3,
C     1     ' IN STRESS PERIOD',I3)
C
C1------USE TIME UNIT INDICATOR TO GET FACTOR TO CONVERT TO SECONDS.
      ZERO=0.
      CNV=ZERO
      IF(ITMUNI.EQ.1) CNV=1.
      IF(ITMUNI.EQ.2) CNV=60.
      IF(ITMUNI.EQ.3) CNV=3600.
      IF(ITMUNI.EQ.4) CNV=86400.
      IF(ITMUNI.EQ.5) CNV=31557600.
C
C2------IF FACTOR=0 THEN TIME UNITS ARE NON-STANDARD.
      IF(CNV.NE.ZERO) GO TO 100
C--SEAWAT: DON'T PRINT TIME STEP LENGTH FOR NOW
C2A-----PRINT TIMES IN NON-STANDARD TIME UNITS.
      WRITE(IOUT,301) DTRANS,HT2-HT1,PERTIM,TOTIM
C      WRITE(IOUT,301) DELT,PERTIM,TOTIM
C2AS1--SEAWAT: CONVERTING FROM 'TIME' TO 'TRANSPORT'
cwxg
  301 FORMAT(21X,'TRANSPORT STEP LENGTH =',G15.6/
     1       21X,'   TIME  STEP  LENGTH =',G15.6/
     2       21X,'   STRESS PERIOD TIME =',G15.6/
     3       21X,'TOTAL SIMULATION TIME =',G15.6)

C
C2A-----PRINT TIMES IN NON-STANDARD TIME UNITS.
C      WRITE(IOUT,301) DELT,PERTIM,TOTIM
C  301 FORMAT(21X,'     TIME STEP LENGTH =',G15.6/
C     1       21X,'   STRESS PERIOD TIME =',G15.6/
C     2       21X,'TOTAL SIMULATION TIME =',G15.6)
C
C2B-----RETURN
      RETURN
C
C3------CALCULATE LENGTH OF TIME STEP & ELAPSED TIMES IN SECONDS.
C--SEAWAT: ADD TRANSPORT STEP LENGTH
C  100 DELSEC=CNV*DELT
C      DELSEC=CNV*DELT
  100 TRNSEC=CNV*DTRANS
      DELSEC=CNV*(HT2-HT1)
      TOTSEC=CNV*TOTIM
      PERSEC=CNV*PERTIM
C
C4------CALCULATE TIMES IN MINUTES,HOURS,DAYS AND YEARS.
      SIXTY=60.
      HRDAY=24.
      DAYYR=365.25

C--SEAWAT: ADD TRNMN, TRNHR, TRNDY, TRNYR
      TRNMN=TRNSEC/SIXTY
      TRNHR=TRNMN/SIXTY
      TRNDY=TRNHR/HRDAY
      TRNYR=TRNDY/DAYYR
 
      DELMN=DELSEC/SIXTY
      DELHR=DELMN/SIXTY
      DELDY=DELHR/HRDAY
      DELYR=DELDY/DAYYR
      TOTMN=TOTSEC/SIXTY
      TOTHR=TOTMN/SIXTY
      TOTDY=TOTHR/HRDAY
      TOTYR=TOTDY/DAYYR
      PERMN=PERSEC/SIXTY
      PERHR=PERMN/SIXTY
      PERDY=PERHR/HRDAY
      PERYR=PERDY/DAYYR
C
C5------PRINT TIME STEP LENGTH AND ELAPSED TIMES IN ALL TIME UNITS.
      WRITE(IOUT,200)
  200 FORMAT(19X,' SECONDS     MINUTES      HOURS',7X,
     1    'DAYS        YEARS'/20X,59('-'))

C5S1--SEAWAT: CONVERTING FROM 'TIME STEP' TO 'TRANSPORT STEP'
      WRITE (IOUT,204) TRNSEC,TRNMN,TRNHR,TRNDY,TRNYR
  204 FORMAT(2X,'TRANS STEP LENGTH',1P,5G12.5)

      WRITE (IOUT,201) DELSEC,DELMN,DELHR,DELDY,DELYR
  201 FORMAT(1X,'  TIME STEP LENGTH',1P,5G12.5)
      WRITE(IOUT,202) PERSEC,PERMN,PERHR,PERDY,PERYR
  202 FORMAT(1X,'STRESS PERIOD TIME',1P,5G12.5)
      WRITE(IOUT,203) TOTSEC,TOTMN,TOTHR,TOTDY,TOTYR
  203 FORMAT(1X,'        TOTAL TIME',1P,5G12.5)
C
C6------RETURN
      RETURN
      END






      SUBROUTINE SVDF1BAS6V(MSUM,VBNM,VBVL,KSTP,KPER,IOUT)
C
C---SEAWAT: MODIFIED ON 10/10/02
C-----VERSION 11JAN2000 SGWF1BAS6V
C     ******************************************************************
C     PRINT VOLUMETRIC BUDGET
C--SEAWAT PRINT MASS BUDGET
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
      CHARACTER*16 VBNM(MSUM)
      DIMENSION VBVL(4,MSUM)
      CHARACTER*17 VAL1,VAL2
C     ------------------------------------------------------------------
C
C1------DETERMINE NUMBER OF INDIVIDUAL BUDGET ENTRIES.
      MSUM1=MSUM-1
      IF(MSUM1.LE.0) RETURN
C
C2------CLEAR RATE AND VOLUME ACCUMULATORS.
      ZERO=0.
      TWO=2.
      HUND=100.
      BIGVL1=9.99999E11
      BIGVL2=9.99999E10
      SMALL=0.1
      TOTRIN=ZERO
      TOTROT=ZERO
      TOTVIN=ZERO
      TOTVOT=ZERO
C
C3------ADD RATES AND VOLUMES (IN AND OUT) TO ACCUMULATORS.
      DO 100 L=1,MSUM1
      TOTRIN=TOTRIN+VBVL(3,L)
      TOTROT=TOTROT+VBVL(4,L)
      TOTVIN=TOTVIN+VBVL(1,L)
      TOTVOT=TOTVOT+VBVL(2,L)
  100 CONTINUE
C
C4------PRINT TIME STEP NUMBER AND STRESS PERIOD NUMBER.
      WRITE(IOUT,260) KSTP,KPER
      WRITE(IOUT,265)
C
C5------PRINT INDIVIDUAL INFLOW RATES AND VOLUMES AND THEIR TOTALS.
      DO 200 L=1,MSUM1
       IF(VBVL(1,L).NE.ZERO .AND.
     1       (VBVL(1,L).GE.BIGVL1 .OR. VBVL(1,L).LT.SMALL)) THEN
         WRITE(VAL1,'(1PE17.4)') VBVL(1,L)
       ELSE
         WRITE(VAL1,'(F17.4)') VBVL(1,L)
       END IF
       IF(VBVL(3,L).NE.ZERO .AND.
     1       (VBVL(3,L).GE.BIGVL1 .OR. VBVL(3,L).LT.SMALL)) THEN
         WRITE(VAL2,'(1PE17.4)') VBVL(3,L)
       ELSE
         WRITE(VAL2,'(F17.4)') VBVL(3,L)
       END IF
       WRITE(IOUT,275) VBNM(L),VAL1,VBNM(L),VAL2
  200 CONTINUE
      IF(TOTVIN.NE.ZERO .AND.
     1      (TOTVIN.GE.BIGVL1 .OR. TOTVIN.LT.SMALL)) THEN
         WRITE(VAL1,'(1PE17.4)') TOTVIN
      ELSE
         WRITE(VAL1,'(F17.4)') TOTVIN
      END IF
      IF(TOTRIN.NE.ZERO .AND.
     1      (TOTRIN.GE.BIGVL1 .OR. TOTRIN.LT.SMALL)) THEN
         WRITE(VAL2,'(1PE17.4)') TOTRIN
      ELSE
         WRITE(VAL2,'(F17.4)') TOTRIN
      END IF
      WRITE(IOUT,286) VAL1,VAL2
C
C6------PRINT INDIVIDUAL OUTFLOW RATES AND VOLUMES AND THEIR TOTALS.
      WRITE(IOUT,287)
      DO 250 L=1,MSUM1
       IF(VBVL(2,L).NE.ZERO .AND.
     1       (VBVL(2,L).GE.BIGVL1 .OR. VBVL(2,L).LT.SMALL)) THEN
         WRITE(VAL1,'(1PE17.4)') VBVL(2,L)
       ELSE
         WRITE(VAL1,'(F17.4)') VBVL(2,L)
       END IF
       IF(VBVL(4,L).NE.ZERO .AND.
     1       (VBVL(4,L).GE.BIGVL1 .OR. VBVL(4,L).LT.SMALL)) THEN
         WRITE(VAL2,'(1PE17.4)') VBVL(4,L)
       ELSE
         WRITE(VAL2,'(F17.4)') VBVL(4,L)
       END IF
       WRITE(IOUT,275) VBNM(L),VAL1,VBNM(L),VAL2
  250 CONTINUE
      IF(TOTVOT.NE.ZERO .AND.
     1      (TOTVOT.GE.BIGVL1 .OR. TOTVOT.LT.SMALL)) THEN
         WRITE(VAL1,'(1PE17.4)') TOTVOT
      ELSE
         WRITE(VAL1,'(F17.4)') TOTVOT
      END IF
      IF(TOTROT.NE.ZERO .AND.
     1      (TOTROT.GE.BIGVL1 .OR. TOTROT.LT.SMALL)) THEN
         WRITE(VAL2,'(1PE17.4)') TOTROT
      ELSE
         WRITE(VAL2,'(F17.4)') TOTROT
      END IF
      WRITE(IOUT,298) VAL1,VAL2
C
C7------CALCULATE THE DIFFERENCE BETWEEN INFLOW AND OUTFLOW.
C
C7A-----CALCULATE DIFFERENCE BETWEEN RATE IN AND RATE OUT.
      DIFFR=TOTRIN-TOTROT
      ADIFFR=ABS(DIFFR)
C
C7B-----CALCULATE PERCENT DIFFERENCE BETWEEN RATE IN AND RATE OUT.
      PDIFFR=ZERO
      AVGRAT=(TOTRIN+TOTROT)/TWO
      IF(AVGRAT.NE.ZERO) PDIFFR=HUND*DIFFR/AVGRAT
C
C7C-----CALCULATE DIFFERENCE BETWEEN VOLUME IN AND VOLUME OUT.
      DIFFV=TOTVIN-TOTVOT
      ADIFFV=ABS(DIFFV)
C
C7D-----GET PERCENT DIFFERENCE BETWEEN VOLUME IN AND VOLUME OUT.
      PDIFFV=ZERO
      AVGVOL=(TOTVIN+TOTVOT)/TWO
      IF(AVGVOL.NE.ZERO) PDIFFV=HUND*DIFFV/AVGVOL
C
C8------PRINT DIFFERENCES AND PERCENT DIFFERENCES BETWEEN INPUT
C8------AND OUTPUT RATES AND VOLUMES.
      IF(ADIFFV.NE.ZERO .AND.
     1      (ADIFFV.GE.BIGVL2 .OR. ADIFFV.LT.SMALL)) THEN
         WRITE(VAL1,'(1PE17.4)') DIFFV
      ELSE
         WRITE(VAL1,'(F17.4)') DIFFV
      END IF
      IF(ADIFFR.NE.ZERO .AND.
     1      (ADIFFR.GE.BIGVL2 .OR. ADIFFR.LT.SMALL)) THEN
         WRITE(VAL2,'(1PE17.4)') DIFFR
      ELSE
         WRITE(VAL2,'(F17.4)') DIFFR
      END IF
      WRITE(IOUT,299) VAL1,VAL2
      WRITE(IOUT,300) PDIFFV,PDIFFR
C
C9------RETURN.
      RETURN
C
C    ---FORMATS
C
C--SEAWAT: TITLES CHANGED TO MASS HERE
  260 FORMAT('1',/2X,'MASS BUDGET FOR ENTIRE MODEL AT END OF'
     1,' TIME STEP',I3,' IN STRESS PERIOD',I4/2X,78('-'))
  265 FORMAT(1X,/5X,'CUMULATIVE MASS',6X,'M',7X
     1,'RATES FOR THIS TIME STEP',6X,'M/T'/5X,18('-'),17X,24('-')
     2//11X,'IN:',38X,'IN:'/11X,'---',38X,'---')
  275 FORMAT(1X,3X,A16,' =',A17,6X,A16,' =',A17)
  286 FORMAT(1X,/12X,'TOTAL IN =',A,14X,'TOTAL IN =',A)
  287 FORMAT(1X,/10X,'OUT:',37X,'OUT:'/10X,4('-'),37X,4('-'))
  298 FORMAT(1X,/11X,'TOTAL OUT =',A,13X,'TOTAL OUT =',A)
  299 FORMAT(1X,/12X,'IN - OUT =',A,14X,'IN - OUT =',A)
  300 FORMAT(1X,/1X,'PERCENT DISCREPANCY =',F15.2
     1,5X,'PERCENT DISCREPANCY =',F15.2,///)
C
      END