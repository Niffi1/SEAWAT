C
      SUBROUTINE VDF1EVT6FM(NEVTOP,IEVT,EVTR,EXDP,SURF,RHS,HCOF,
     1                  IBOUND,HNEW,NCOL,NROW,NLAY,
     2                  CEVT,NCOMP)
C
C-----VERSION 14DEC2000 GWF1EVT6FM
C     ******************************************************************
C        ADD EVAPOTRANSPIRATION TO RHS AND HCOF
C--SEAWAT: INCLUDES VARIABLE-DENSITY MODIFICATIONS
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: MT3DRHOFLG,DENSEREF,PS,ELEV
C
      DOUBLE PRECISION HNEW,HH,SS,XX,DD
      DIMENSION IEVT(NCOL,NROW),EVTR(NCOL,NROW),EXDP(NCOL,NROW),
     1          SURF(NCOL,NROW),RHS(NCOL,NROW,NLAY),
     2          HCOF(NCOL,NROW,NLAY),IBOUND(NCOL,NROW,NLAY),
     3          HNEW(NCOL,NROW,NLAY)
C--SEAWAT: DIMENSION ADDITIONAL VARIABLES
      DIMENSION CEVT(NCOL,NROW,NCOMP)
C     ------------------------------------------------------------------
C
C1------PROCESS EACH HORIZONTAL CELL LOCATION
      DO 10 IR=1,NROW
      DO 10 IC=1,NCOL
C
C2------SET THE LAYER INDEX -- FOR OPTION 1, THE LAYER IS 1;
C2------FOR OPTION 2, THE LAYER IS SPECIFIED IN IEVT.
      IF(NEVTOP.EQ.1) THEN
         IL=1
      ELSE IF(NEVTOP.EQ.2) THEN
         IL=IEVT(IC,IR)
         IF(IL.EQ.0) GO TO 10  ! ERB 1/11/07
      ELSE
C
C3------FOR OPTION 3, FIND UPPERMOST ACTIVE CELL.
         DO 3 IL=1,NLAY
		IF(IBOUND(IC,IR,IL).NE.0) GO TO 4
    3    CONTINUE
         IL=1
      END IF
C
C4------IF THE CELL IS EXTERNAL IGNORE IT.
    4 IF(IBOUND(IC,IR,IL).LE.0)GO TO 10
C--SEAWAT: DETERMINE DENSE VALUE OF WITHDRAWN ET FLUID
            DENSE=DENSEREF
            IF(MT3DRHOFLG.NE.0) THEN
		        DENSE=PS(IC,IR,IL)
		        DENSEEVT=CALCDENS(IC,IR,IL,CEVT(IC,IR,1:NCOMP))
		        IF(DENSEEVT.LT.DENSE) DENSE=DENSEEVT
            ENDIF
      C=EVTR(IC,IR)
      S=SURF(IC,IR)
      SS=S
C      HH=HNEW(IC,IR,IL)
      HH=SALTHEAD(HNEW(IC,IR,IL),PS(IC,IR,IL),ELEV(IC,IR,IL))
C
C5------IF AQUIFER HEAD IS GREATER THAN OR EQUAL TO SURF, ET IS CONSTANT
      IF(HH.LT.SS) GO TO 5
C
C5A-----SUBTRACT -EVTR FROM RHS
C--SEAWAT: CONSERVE MASS
C      RHS(IC,IR,IL)=RHS(IC,IR,IL) + C
      RHS(IC,IR,IL)=RHS(IC,IR,IL) + C*DENSE

      GO TO 10
C
C6------IF DEPTH TO WATER>=EXTINCTION DEPTH THEN ET IS 0
    5 DD=SS-HH
      X=EXDP(IC,IR)
      XX=X
      IF(DD.GE.XX)GO TO 10
C
C7------LINEAR RANGE. ADD ET TERMS TO BOTH RHS AND HCOF.
C--SEAWAT: USE REFORMULATED EQUATION, CONSERVE MASS
C     RHS(IC,IR,IL)=RHS(IC,IR,IL)+C-C*S/X
C     HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-C/X
      RHS(IC,IR,IL)=RHS(IC,IR,IL)+DENSE*C-DENSE*C*S/X
      RHS(IC,IR,IL)=RHS(IC,IR,IL)+DENSE*C/X*(PS(IC,IR,IL)-DENSEREF)/
     +              PS(IC,IR,IL)*ELEV(IC,IR,IL)
      HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-DENSE*DENSEREF/PS(IC,IR,IL)*C/X
   10 CONTINUE
C
C8------RETURN
      RETURN
      END
C
      SUBROUTINE VDF1EVT6BD(NEVTOP,IEVT,EVTR,EXDP,SURF,IBOUND,HNEW,
     1           NCOL,NROW,NLAY,DELT,VBVL,VBNM,MSUM,KSTP,KPER,
     2           IEVTCB,ICBCFL,BUFF,IOUT,PERTIM,TOTIM,CEVT,NCOMP)
C-----VERSION 14DEC2000 GWF1EVT6BD
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR EVAPOTRANSPIRATION
C--SEAWAT: MODIFIED 05-06-2003
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      USE VDFMODULE,   ONLY: MT3DRHOFLG,DENSEREF,PS,ELEV
C
      CHARACTER*16 VBNM(MSUM),TEXT
      DOUBLE PRECISION HNEW,RATOUT,QQ,HH,SS,DD,XX,HHCOF,RRHS
      DIMENSION IEVT(NCOL,NROW),EVTR(NCOL,NROW),EXDP(NCOL,NROW),
     1          SURF(NCOL,NROW),IBOUND(NCOL,NROW,NLAY),
     2          VBVL(4,MSUM),HNEW(NCOL,NROW,NLAY),BUFF(NCOL,NROW,NLAY)
C
      DATA TEXT /'              ET'/
C--SEAWAT: DIMENSION ADDITIONAL ARRAYS
      DIMENSION CEVT(NCOL,NROW,NCOMP)
C     ------------------------------------------------------------------
C
C1------CLEAR THE RATE ACCUMULATOR.
      ZERO=0.
      RATOUT=ZERO
C
C2------SET CELL-BY-CELL BUDGET SAVE FLAG (IBD) AND CLEAR THE BUFFER.
      IBD=0
      IF(IEVTCB.GT.0) IBD=ICBCFL
      DO 2 IL=1,NLAY
      DO 2 IR=1,NROW
      DO 2 IC=1,NCOL
      BUFF(IC,IR,IL)=ZERO
    2 CONTINUE
C
C3------PROCESS EACH HORIZONTAL CELL LOCATION.
      DO 10 IR=1,NROW
      DO 10 IC=1,NCOL
C
C4------SET THE LAYER INDEX -- FOR OPTION 1, THE LAYER IS 1;
C4------FOR OPTION 2, THE LAYER IS SPECIFIED IN IEVT.
      IF(NEVTOP.EQ.1) THEN
         IL=1
      ELSE IF(NEVTOP.EQ.2) THEN
         IL=IEVT(IC,IR)
         IF(IL.EQ.0) GO TO 10  ! ERB 1/11/07
      ELSE

C
C5------FOR OPTION 3, FIND UPPERMOST ACTIVE CELL.
         DO 3 IL=1,NLAY
         IF(IBOUND(IC,IR,IL).NE.0) GO TO 4
    3    CONTINUE
         IL=1
    4    IEVT(IC,IR)=IL
      END IF

C
C6------IF CELL IS EXTERNAL THEN IGNORE IT.
      IF(IBOUND(IC,IR,IL).LE.0)GO TO 10
C--SEAWAT: DETERMINE DENSE VALUE OF WITHDRAWN ET FLUID
            DENSE=DENSEREF
            IF(MT3DRHOFLG.NE.0) THEN
		        DENSE=PS(IC,IR,IL)
		        DENSEEVT=CALCDENS(IC,IR,IL,CEVT(IC,IR,1:NCOMP))
		        IF(DENSEEVT.LT.DENSE) DENSE=DENSEEVT
            ENDIF
      C=EVTR(IC,IR)
      S=SURF(IC,IR)
      SS=S
C      HH=HNEW(IC,IR,IL)
      HH=SALTHEAD(HNEW(IC,IR,IL),PS(IC,IR,IL),ELEV(IC,IR,IL))
C
C7------IF AQUIFER HEAD => SURF,SET Q=MAX ET RATE.
      IF(HH.LT.SS) GO TO 7
C--SEAWAT:CONSERVE MASS
C      QQ=-C
      QQ=-C*DENSE
      GO TO 9
C
C8------IF DEPTH=>EXTINCTION DEPTH, ET IS 0.
    7 X=EXDP(IC,IR)
      XX=X
      DD=SS-HH
      IF(DD.GE.XX)GO TO 10
C
C9------LINEAR RANGE.  Q=-EVTR*(HNEW-(SURF-EXDP))/EXDP, WHICH IS
C9------FORMULATED AS Q= -HNEW*EVTR/EXDP + (EVTR*SURF/EXDP -EVTR).
      HHCOF=-C/X
      RRHS=(C*S/X)-C
C--SEAWAT:CONSERVE MASS
C      QQ=HH*HHCOF+RRHS
      QQ=(HH*HHCOF+RRHS)*DENSE
C
C10-----ACCUMULATE TOTAL FLOW RATE.
    9 Q=QQ
      RATOUT=RATOUT-QQ
C
C11-----ADD Q TO BUFFER.
C--SEAWAT:STORE AS VOLUME
C      BUFF(IC,IR,IL)=Q
      BUFF(IC,IR,IL)=Q/DENSE
   10 CONTINUE
C
C12-----IF CELL-BY-CELL FLOW TO BE SAVED, CALL APPROPRIATE UTILITY
C12-----MODULE SAVE THEM.
      IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IEVTCB,BUFF,NCOL,NROW,
     1                          NLAY,IOUT)
      IF(IBD.EQ.2) CALL UBDSV3(KSTP,KPER,TEXT,IEVTCB,BUFF,IEVT,NEVTOP,
     1                   NCOL,NROW,NLAY,IOUT,DELT,PERTIM,TOTIM,IBOUND)
C
C13-----MOVE TOTAL ET RATE INTO VBVL FOR PRINTING BY BAS1OT.
      ROUT=RATOUT
      VBVL(3,MSUM)=ZERO
      VBVL(4,MSUM)=ROUT
C
C14-----ADD ET(ET_RATE TIMES STEP LENGTH) TO VBVL.
      VBVL(2,MSUM)=VBVL(2,MSUM)+ROUT*DELT
C
C15-----MOVE BUDGET TERM LABELS TO VBNM FOR PRINT BY MODULE BAS1OT.
      VBNM(MSUM)=TEXT
C
C16-----INCREMENT BUDGET TERM COUNTER.
      MSUM=MSUM+1
C
C17-----RETURN.
      RETURN
      END

