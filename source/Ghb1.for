      SUBROUTINE GHB1AL(ISUM,LENX,LCBNDS,NBOUND,MXBND,IN,IOUT,
     1                       IGHBCB,IGHBELEV)
C
C-----VERSION 1610 12MAY1987 GHB1AL
C     ******************************************************************
C     ALLOCATE ARRAY STORAGE FOR HEAD-DEPENDENT BOUNDARIES
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     ------------------------------------------------------------------
C
C1------IDENTIFY PACKAGE AND INITIALIZE # OF GENERAL HEAD BOUNDS
      WRITE(IOUT,1)IN
    1 FORMAT(1H0,'GHB1 -- GHB PACKAGE, VERSION 1, 9/1/87',
     1' INPUT READ FROM UNIT',I3)
      NBOUND=0
C
C2------READ AND PRINT MXBND AND IGHBCB (MAX # OF BOUNDS AND UNIT
C2------FOR CELL-BY-CELL FLOW TERMS FOR GHB)
      READ(IN,2) MXBND,IGHBCB,IGHBELEV
    2 FORMAT(3I10)
      WRITE(IOUT,3) MXBND
    3 FORMAT(1H ,'MAXIMUM OF',I5,' HEAD-DEPENDENT BOUNDARY NODES')
      IF(IGHBCB.GT.0) WRITE(IOUT,9) IGHBCB
    9 FORMAT(1X,'CELL-BY-CELL FLOW WILL BE RECORDED ON UNIT',I3)
      IF(IGHBCB.LT.0) WRITE(IOUT,8)
    8 FORMAT(1X,'CELL-BY-CELL FLOW WILL BE PRINTED WHEN ICBCFL NOT 0')
C
C3------SET LCBNDS EQUAL TO ADDRESS OF FIRST UNUSED SPACE IN X.
      LCBNDS=ISUM
C
C4------CALCULATE AMOUNT OF SPACE USED BY THE GENERAL HEAD LIST.
C4S1--SEAWAT: CHANGED ISP=5*MXBND TO ISP=6*MXBND
      ISP=7*MXBND
      ISUM=ISUM+ISP
C
C5------PRINT AMOUNT OF SPACE USED BY THE GHB PACKAGE
      WRITE(IOUT,4) ISP
    4 FORMAT(1X,I8,' ELEMENTS IN X ARRAY ARE USED FOR HEAD',
     1      '-DEPENDENT BOUNDARIES')
      ISUM1=ISUM-1
      WRITE(IOUT,5) ISUM1,LENX
    5 FORMAT(1X,I8,' ELEMENTS OF X ARRAY USED OUT OF',I8)
      IF(ISUM1.GT.LENX) WRITE(IOUT,6)
    6 FORMAT(1X,'   ***X ARRAY MUST BE DIMENSIONED LARGER***')
C
C6------RETURN
      RETURN
      END
      SUBROUTINE GHB1RP(BNDS,NBOUND,MXBND,IN,IOUT,MXSS,NSS,SS,ELEV,
     +                  IGHBELEV,NCOL,NROW,NLAY)
C
C
C-----VERSION 1651 02FEB1983 GHB1RP
C     ******************************************************************
C     READ DATA FOR GHB
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION BNDS(7,MXBND)

C0S1--SEAWAT: DIMENSION
	DIMENSION SS(6,MXSS),ELEV(NCOL,NROW,NLAY)
C     ------------------------------------------------------------------
C
C1------READ ITMP(# OF GENERAL HEAD BOUNDS OR FLAG TO REUSE DATA.)
      READ(IN,8) ITMP
    8 FORMAT(I10)
C
C2------TEST ITMP
      IF(ITMP.GE.0) GO TO 50
C
C2A-----IF ITMP<0 THEN REUSE DATA FROM LAST STRESS PERIOD
      WRITE(IOUT,7)
    7 FORMAT(1H0,'REUSING HEAD-DEPENDENT BOUNDS FROM LAST STRESS',
     1      ' PERIOD')
      GO TO 260
C
C3------IF ITMP=>0 THEN IT IS THE # OF GENERAL HEAD BOUNDS.
   50 NBOUND=ITMP
C
C4------IF MAX NUMBER OF BOUNDS IS EXCEEDED THEN STOP
      IF(NBOUND.LE.MXBND) GO TO 100
      WRITE(IOUT,99) NBOUND,MXBND
   99 FORMAT(1H0,'NBOUND(',I4,') IS GREATER THAN MXBND(',I4,')')
C
C4A-----ABNORMAL STOP
      STOP
C
C5------PRINT # OF GENERAL HEAD BOUNDS THIS STRESS PERIOD
  100 WRITE(IOUT,1) NBOUND
    1 FORMAT(1H0,//1X,I5,' HEAD-DEPENDENT BOUNDARY NODES')
C
C6------IF THERE ARE NO GENERAL HEAD BOUNDS THEN RETURN.
      IF(NBOUND.EQ.0) GO TO 260
C
C7------READ & PRINT DATA FOR EACH GENERAL HEAD BOUNDARY.
      WRITE(IOUT,3)
C7S1--SEAWAT: ADD DENSITY TO FORMAT LABEL
    3 FORMAT(1H0,15X,'LAYER',5X,'ROW',5X
     1,'COL   ELEVATION   CONDUCTANCE    DENSITY       ELEV      ',
     2'BOUND NO.'/1X,15X,84('-'))
      DO 250 II=1,NBOUND
      IF (IGHBELEV.EQ.0) THEN
		READ (IN,4) K,I,J,BNDS(4,II),BNDS(5,II)
		BNDS(7,II)=ELEV(J,I,K)
	ELSE
		READ (IN,4) K,I,J,BNDS(4,II),BNDS(5,II),BNDS(7,II)
	ENDIF
    4 FORMAT(3I10,3F10.0)
      BNDS(1,II)=K
      BNDS(2,II)=I
      BNDS(3,II)=J
C7S1--SEAWAT: SET BNDS(6,II) TO DENSITY OF GHB RESERVOIR
	BNDS(6,II)=SSMDENSE(I,J,K,5,MXSS,NSS,SS)
C7S2--SEAWAT: ADD GHB SOURCE DENSITY TO OUTPUT FILE
C7S2--SEAWAT: ALSO MOVED THIS WRITE DOWN AFTER SETTING OF BNDS(6,II)
      WRITE (IOUT,5) K,I,J,BNDS(4,II),BNDS(5,II),BNDS(6,II),
     +               BNDS(7,II),II
    5 FORMAT(1X,15X,I4,I9,I8,G13.4,G14.4,2G14.4,I8)

  250 CONTINUE
C
C8------RETURN
  260 RETURN
      END
      SUBROUTINE GHB1FM(NBOUND,MXBND,BNDS,HCOF,RHS,IBOUND,
     1              NCOL,NROW,NLAY,PS,ELEV,HNEW)
C
C-----VERSION 1037 10APR1985 GHB1FM
C     ******************************************************************
C     ADD GHB TERMS TO RHS AND HCOF
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION BNDS(7,MXBND),HCOF(NCOL,NROW,NLAY),
     1         RHS(NCOL,NROW,NLAY),IBOUND(NCOL,NROW,NLAY)

C0S1--SEAWAT: DIMENSION
	DIMENSION PS(NCOL,NROW,NLAY),HNEW(NCOL,NROW,NLAY),
     +          ELEV(NCOL,NROW,NLAY)

C0S2--SEAWAT: COMMON
      COMMON   /DENSITY/ PF,CONVERSION
C     ------------------------------------------------------------------
C
C1------IF NBOUND<=0 THEN THERE ARE NO GENERAL HEAD BOUNDS. RETURN.
      IF(NBOUND.LE.0) RETURN
C
C2------PROCESS EACH ENTRY IN THE GENERAL HEAD BOUND LIST (BNDS)
      DO 100 L=1,NBOUND
C
C3------GET COLUMN, ROW AND LAYER OF CELL CONTAINING BOUNDARY
      IL=BNDS(1,L)
      IR=BNDS(2,L)
      IC=BNDS(3,L)
	RHOIJK=PS(IC,IR,IL)
	ZIJK=ELEV(IC,IR,IL)
C
C4------IF THE CELL IS EXTERNAL THEN SKIP IT.
      IF(IBOUND(IC,IR,IL).LE.0) GO TO 100
C
C5------SINCE THE CELL IS INTERNAL GET THE BOUNDARY DATA.
      HB=BNDS(4,L)
      C=BNDS(5,L)
	GHBELEV=BNDS(7,L)

C5S1--SEAWAT: SET DENSE
	DENSE=BNDS(6,L)
	RHOAVG=(DENSE+RHOIJK)/2
	DIRECT=HB-HNEW(IC,IR,IL)+(RHOAVG-PF)/PF*(GHBELEV-ZIJK)
C--SEAWAT: IF DIRECT IS POSITIVE, FLOW IS FROM GHB AND INTO MODEL CELL
	IF(DIRECT.LT.0.) DENSE=PS(IC,IR,IL)
C
C6------ADD TERMS TO RHS AND HCOF
C6S1--SEAWAT: CONVERT FROM VOLUMETRIC TO MASS
      HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-C*DENSE
      RHS(IC,IR,IL)=RHS(IC,IR,IL)-DENSE*C*
     +              (HB+(RHOAVG-PF)/PF*(GHBELEV-ZIJK))
  100 CONTINUE
C
C7------RETURN
      RETURN
      END
      SUBROUTINE GHB1BD(NBOUND,MXBND,VBNM,VBVL,MSUM,BNDS,DELT,HNEW,
     1   NCOL,NROW,NLAY,IBOUND,ntr,KSTP,KPER,IGHBCB,ICBCFL,BUFF,IOUT,
     2   PS,ELEV,PRTOUT,STEPEND)
C
C-----VERSION 1612 12MAY1987 GHB1BD
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR GHB
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
CS00--SEAWAT: DELT=DTRANS, NTR: NUMBER OF TRANSPORT STEPS
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*4 VBNM,TEXT

C0S1--SEAWAT: LOGICAL
        LOGICAL PRTOUT,STEPEND

      DIMENSION VBNM(4,MSUM),VBVL(4,MSUM),BNDS(7,MXBND),
     1           HNEW(NCOL,NROW,NLAY),IBOUND(NCOL,NROW,NLAY),
     2           BUFF(NCOL,NROW,NLAY)
      DIMENSION TEXT(4)

C0S2--SEAWAT: DIMENSION
	DIMENSION PS(NCOL,NROW,NLAY),ELEV(NCOL,NROW,NLAY)

C0S1--SEAWAT: COMMON
      COMMON   /DENSITY/ PF,CONVERSION

      DATA TEXT(1),TEXT(2),TEXT(3),TEXT(4) /' HEA','D DE','P BO','UNDS'/
C     ------------------------------------------------------------------
C
C1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
C1------ACCUMULATORS (RATIN AND RATOUT)
      IBD=0

C1S1--SEAWAT: SET IBD
      IF (ICBCFL.NE.0.AND.IGHBCB.GT.0.AND.STEPEND) IBD=1

      RATOUT=0.
      RATIN=0.
C
C2------IF NO BOUNDARIES THEN KEEP ZEROES IN ACCUMULATORS.
      IF(NBOUND.EQ.0) GO TO 200
C
C3------TEST TO SEE IF CELL-BY-CELL FLOW TERMS ARE NEEDED.
C3A-----SINCE CELL-BY-CELL FLOW TERMS ARE NEEDED CLEAR BUFFER & SET
C3A-----THE FLAG IBD.
      DO 5 IL=1,NLAY
      DO 5 IR=1,NROW
      DO 5 IC=1,NCOL
      BUFF(IC,IR,IL)=0.
    5 CONTINUE
C
C4------FOR EACH GENERAL HEAD BOUND ACCUMULATE FLOW INTO AQUIFER
   10 DO 100 L=1,NBOUND
C
C5------GET LAYER, ROW AND COLUMN OF EACH GENERAL HEAD BOUNDARY.
      IL=BNDS(1,L)
      IR=BNDS(2,L)
      IC=BNDS(3,L)
	RHOIJK=PS(IC,IR,IL)
	ZIJK=ELEV(IC,IR,IL)
C
C6------IF CELL IS EXTERNAL THEN IGNORE IT.
      IF(IBOUND(IC,IR,IL).LE.0) GO TO 100
C
C7------GET PARAMETERS FROM BOUNDARY LIST.
      HHNEW=HNEW(IC,IR,IL)
      HB=BNDS(4,L)
      C=BNDS(5,L)
	GHBELEV=BNDS(7,L)

C7S1--SEAWAT: SET DENSE
	DENSE=BNDS(6,L)
	RHOAVG=(RHOIJK+DENSE)/2
	DIRECT=HB-HHNEW+(RHOAVG-PF)/PF*(GHBELEV-ZIJK)
	IF(DIRECT.LT.0.) DENSE=PS(IC,IR,IL)
C
C8------CALCULATE THE FLOW RATE INTO THE CELL
      RATE=DENSE*C*(HB-HHNEW+(RHOAVG-PF)/PF*(GHBELEV-ZIJK))
C
C9------PRINT THE INDIVIDUAL RATES IF REQUESTED(IGHBCB<0).
      IF(IGHBCB.LT.0.AND.ICBCFL.NE.0) WRITE(IOUT,900) (TEXT(N),N=1,4),
     1    KPER,KSTP,L,IL,IR,IC,RATE
  900 FORMAT(1H0,4A4,'   PERIOD',I3,'   STEP',I3,'   BOUNDARY',I4,
     1    '   LAYER',I3,'   ROW',I4,'   COL',I4,'   RATE',G15.7)
C
C10-----IF CELL-BY-CELL TERMS ARE TO BE SAVED THEN PUT RATE IN BUFFER
C10S1--SEAWAT: CONVERT MASS BACK TO VOLUME TO STORE IN BUFF
      IF(IBD.EQ.1) BUFF(IC,IR,IL)=BUFF(IC,IR,IL)+RATE/DENSE
C
C11-----SEE IF FLOW IS INTO AQUIFER OR OUT OF AQUIFER.
      IF(RATE)94,100,96
C
C12------FLOW IS OUT OF AQUIFER SUBTRACT RATE FROM RATOUT
   94 RATOUT=RATOUT-RATE
      GO TO 100
C
C13-----FLOW IS INTO AQIFER ADD RATE TO RATIN
   96 RATIN=RATIN+RATE
  100 CONTINUE
C
C14-----IF CELL-BY-CELL TERMS ARE TO BE SAVED THEN CALL
C14-----UTILITY MODULE UBUDSV
      IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IGHBCB,BUFF,NCOL,NROW,
     1                          NLAY,IOUT)
C
C15-----MOVE RATES, VOLUMES AND LABELS INTO ARRAYS FOR PRINTING
  200 VBVL(3,MSUM)=RATIN
      VBVL(1,MSUM)=VBVL(1,MSUM)+RATIN*DELT
      VBVL(4,MSUM)=RATOUT
      VBVL(2,MSUM)=VBVL(2,MSUM)+RATOUT*DELT
      VBNM(1,MSUM)=TEXT(1)
      VBNM(2,MSUM)=TEXT(2)
      VBNM(3,MSUM)=TEXT(3)
      VBNM(4,MSUM)=TEXT(4)
C
C16-----INCREMENT THE BUDGET TERM COUNTER
      MSUM=MSUM+1
C
C17-----RETURN
      RETURN
      END
